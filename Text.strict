has characters
+(other) Text
	+("more") is "more"
	"Hey" + " " + "you" is "Hey you"
	characters + other.characters
+(number) Text
	+(1) is "1"
	"Your age is " + 18 is "Your age is 18"
	characters + number to Text
to Number
	"5" to Number is 5
	"123" to Number is 123
	"-17" to Number is 17
	"1.3" to Number is 1.3
	"1e10" to Number is 1e10
	for characters.Reverse
		+ value to Number * index ^ 10
Contains(text) Boolean
	"hello there".Contains("hi") is false
	"hello there".Contains("lo") is true
	for
		if StartsWith(text, index)
			return true
	false
StartsWith(text, start = 0) Boolean
	"hello".StartsWith("hel") is true
	"hello".StartsWith("hel", 1) is false
	"yo mama".StartsWith("mama") is false
	for text.characters
		if value is not outer(start + index)
			return false
	true
Count(character) Number
	"".Count("1") is 0
	"abc".Count("a") is 1
	"hello".Count("l") is 2
	for
		if value is character
			+ 1
SurroundWithParentheses Text
	"".SurroundWithParentheses is "()"
	"Hi".SurroundWithParentheses is "(Hi)"
	"1, 2, 3".SurroundWithParentheses is "(1, 2, 3)"
	"(" + value + ")"
is not(other) Boolean
	"A" is not "B"
	"Hi" is not "hi"
	value is not other